// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.novoda:bintray-release:0.3.4'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    ARTIFACT_ID = 'AndroidOnboarder'
    VERSION_NAME = '0.5'
    VERSION_CODE = 4
    DESCRIPTION = 'A light-weight library to create beauty onboarding experience in Material Design'
    SITE_URL = 'https://github.com/chyrta'
    GIT_URL = 'https://github.com/chyrta/AndroidOnboarder'
    GROUP_NAME = 'com.github.chyrta'
    MODULE_NAME = 'onboarder'
    COMPILE_SDK = 23
    BUILD_TOOLS = '23.0.3'
    LICENSE = 'MIT'
    IS_UPLOADING = project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}
}

subprojects {
    group = GROUP_NAME
    version = VERSION_CODE

    if (IS_UPLOADING && project.name in [MODULE_NAME]) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                println 'Overriding pom-file to make sure we can sync to maven central!'
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name "$project.name"
                        artifactId ARTIFACT_ID
                        packaging project.name == 'compiler' ? 'jar' : 'aar'
                        description DESCRIPTION
                        url SITE_URL
                        version VERSION_NAME

                        scm {
                            url GIT_URL
                            connection GIT_URL
                            developerConnection GIT_URL
                        }

                        licenses {
                            license {
                                name LICENSE
                            }
                        }

                        developers {
                            developer {
                                id 'chyrta'
                                name 'Dzmitry Chyrta'
                                email 'dima@chyrta.com'
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}